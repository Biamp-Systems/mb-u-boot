echo Setting up Harman AVBX environment
echo -----------------------------------
echo Network Configuration:
echo TODO - Use something for MAC address
setenv autoload no
echo Leaving ethaddr unconfigured
setenv ipaddr 192.168.1.1
setenv serverip 192.168.1.100

setenv crcreturn 1

echo Clobber DRAM Configuration:
setenv clobstart 0x88000000

echo Boot FPGA Configuration:
setenv bootfpgasize 0x200000
setenv bootfpgastart 0x0
setenv bootfpganame Avbx_Fpga-failsafe.bin

echo Bootloader Configuration:
setenv bootsize 0x40000
setenv bootstart 0x200000
setenv bootenvsize 0x40000
setenv bootenvstart 0xE40000

setenv eraseenv 'run spiprobe;sf erase ${bootenvstart} ${bootenvsize}'

echo Flattened Device Tree Configuration:
setenv fdtsize 0x40000
setenv fdtstart 0x240000
setenv fdtname avbx.dtb

echo Kernel Configuration:
setenv kernsize 0x3C0000
setenv kernstart 0x280000

echo Root Filesystem Configuration:
setenv rootfssize 0x200000
setenv rootfsstart 0x640000

echo ROM Filesystem Configuration:
setenv romfssize 0x200000
setenv romfsstart 0x840000

echo FPGA Configuration:
setenv fpgasize 0x200000
setenv fpgastart 0xA40000
setenv fpganame Avbx_Fpga.bin

echo Settings Filesystem Configuration:
setenv settingsfssize 0x200000
setenv settingsfsstart 0xC40000

echo Boot Configuration:
setenv bootcmd 'run spiprobe;sf read ${tempkernstart} ${kernstart} ${kernsize};sf read ${tempfdtstart} ${fdtstart} ${fdtsize};bootm ${tempkernstart} - ${tempfdtstart}'
setenv bootdelay 3

setenv load_boot_fpga 'tftp ${clobstart} ${bootfpganame}'
setenv install_boot_fpga 'run spiprobe;sf erase ${bootfpgastart} ${bootfpgasize};sf write ${clobstart} ${bootfpgastart} ${bootfpgasize}'
setenv update_boot_fpga run load_boot_fpga install_boot_fpga

setenv load_fpga 'tftp ${clobstart} ${fpganame}'
setenv install_fpga 'run spiprobe;sf erase ${fpgastart} ${fpgasize};sf write ${clobstart} ${fpgastart} ${fpgasize}'
setenv update_fpga run load_fpga install_fpga

setenv kernname linux.ub
setenv load_kernel 'tftp ${clobstart} ${kernname}'
setenv install_kernel 'if test ${crcreturn} -eq 0; then run spiprobe;sf erase ${kernstart} ${kernsize};sf write ${clobstart} ${kernstart} ${filesize}; fi'
setenv update_kernel run load_kernel install_kernel

setenv load_uboot 'tftp ${clobstart} u-boot.bin'
setenv install_uboot 'run spiprobe;sf erase ${bootstart} ${bootsize};sf write ${clobstart} ${bootstart} ${filesize}'
setenv update_uboot run load_uboot install_uboot

setenv load_rootfs 'tftp ${clobstart} rootfs.romfs'
setenv install_rootfs 'run spiprobe;sf erase ${rootfsstart} ${rootfssize};sf write ${clobstart} ${rootfsstart} ${filesize}'
setenv update_rootfs run load_rootfs install_rootfs

setenv load_romfs 'tftp ${clobstart} usr.romfs'
setenv install_romfs 'run spiprobe;sf erase ${romfsstart} ${romfssize};sf write ${clobstart} ${romfsstart} ${filesize}'
setenv update_romfs run load_romfs install_romfs

setenv load_fdt 'tftp ${clobstart} ${fdtname}'
setenv install_fdt 'run spiprobe;sf erase ${fdtstart} ${fdtsize};sf write ${clobstart} ${fdtstart} ${filesize}'
setenv update_fdt run load_fdt install_fdt

setenv load_settingsfs 'tftp ${clobstart} settings.jffs2'
setenv install_settingsfs 'sf probe 0:0 40000000 3;sf erase ${settingsfsstart} ${settingsfssize};sf write ${clobstart} ${settingsfsstart} ${filesize}'
setenv update_settingsfs run load_settingsfs install_settingsfs

setenv update_all 'ping ${serverip} ; run update_boot_fpga update_fpga update_uboot update_fdt update_kernel update_rootfs update_romfs update_settingsfs'

echo Shortcuts for Development:
setenv tempkernstart 0x88800000
setenv tempfdtstart 0x88700000
setenv temp_kernel 'tftp ${tempkernstart} ${kernname};tftp ${tempfdtstart} ${fdtname};bootm ${tempkernstart} - ${tempfdtstart}'
setenv spiprobe 'sf probe 0:0 40000000 3'

echo Harman AVBX Firmware Update
setenv blobname avbxUpdate.bin
setenv blobrunstart 0x88000040
setenv update_blob 'if test ${crcreturn} -eq 0; then source ${blobrunstart}; fi'

echo Saving Configurations...
saveenv

echo Configuration Completed
